apply plugin: 'war'
apply plugin: 'eclipse-wtp'

// Dependencies
repositories { mavenCentral(); }

dependencies {
    providedCompile group:'javax.servlet', name:'javax.servlet-api', version:'3.1.0'
    testCompile 'junit:junit:4.12'
}

sourceCompatibility = 1.7

// Set the Eclipse facets to use 3.1 of the Dynamic Web Module which requires Java 1.7 by default.
eclipse.wtp.facet {
    // Clear default facets to work around bug where you get duplicates including wst 2.4
    facets = []
    facet name: 'jst.java', type: 'fixed'
    facet name: 'jst.web', type: 'fixed'
    facet name: 'jst.web', version: '3.1'
    facet name: 'jst.java', version: '1.7'
}

// Copy WAR into the local usr dir
task publishWar(dependsOn: 'jar', type: Copy) {
    from(war)
    into('../functionaltest-wlpcfg/servers/functionalTestSample/apps')
}

assemble.dependsOn('publishWar')

test {
    reports.html.destination = file("$buildDir/reports/unit")
    reports.junitXml.destination = file("$buildDir/test-results/unit")
}

// Add the FVTs as separate build files so that it is clear what is required for each

// There is an issue with Gradle whereby you are only allowed buildscript{} blocks in the root file
// so as we use the Liberty Gradle plugin in one of the examples we need to include it here.  This
// build code is only required for the "fvtBuild" example 
buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'Sonatype Nexus Snapshots'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:1.0-SNAPSHOT'
    }
}

apply from: 'fvtBuild.gradle'
apply from: 'fvtRule.gradle'